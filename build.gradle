/*
 * Checkstyle-Addons - Additional Checkstyle checks
 * Copyright (c) 2015-2020, the Checkstyle Addons contributors
 *
 * This program is free software: you can redistribute it and/or modify it under the
 * terms of the GNU General Public License, version 3, as published by the Free
 * Software Foundation.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY
 * WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A
 * PARTICULAR PURPOSE. See the GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License along with this
 * program.  If not, see <http://www.gnu.org/licenses/>.
 */

plugins {
    id 'java-library'
    id 'jacoco'
    id 'checkstyle'
    id 'idea'
    id 'maven-publish'
    alias checkstyleAddonsLibs.plugins.spotbugs
    id 'com.jfrog.bintray' version '1.8.4'
    id 'org.barfuin.gradle.taskinfo' version '1.3.1'
    id 'com.thomasjensen.checkstyle.addons.build'
}

repositories {
    mavenCentral()
}


// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
//   Project metadata
// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

group = 'com.thomasjensen.checkstyle.addons'
description = 'Additional Checkstyle Checks'
// version set by build plugin based on version.properties file

checkstyleAddons {
    authorName = 'Thomas Jensen'
    buildTimestamp = new Date()
    checksPackage = 'com/thomasjensen/checkstyle/addons/checks'
    github = 'checkstyle-addons/checkstyle-addons'
    issueTrackerUrl = github.map { "https://github.com/${it}/issues" }
    longName = 'Checkstyle Addons'
    orgName = 'Checkstyle Addons'
    orgUrl = 'https://github.com/checkstyle-addons'
    sqPackage = 'com/thomasjensen/checkstyle/addons/sonarqube'
    sqPluginKey = 'checkstyleaddons'
    website = 'https://checkstyle-addons.thomasjensen.com/'
}

project.gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.hasTask(':classes')) {
        logger.lifecycle("\nBuilding ${project.name} ${project.version}")
    }
}


// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
//   Dependencies of the 'default' dependency configuration, which is also used by the IDE
// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

dependencies {
    generalCompileOnly checkstyleAddonsLibs.jcip
    generalCompileOnly checkstyleAddonsLibs.spotbugs.annotations
    generalCompileOnly checkstyleAddonsLibs.jsr305

    api group: 'com.puppycrawl.tools', name: 'checkstyle', version: checkstyleAddonsLibs.versions.checkstyleBase.get()
    api group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.13.2'

    sonarqubeImplementation group: 'org.codehaus.sonar', name: 'sonar-plugin-api', version: '3.0'
    sonarqubeRuntimeOnly group: 'org.slf4j', name: 'slf4j-nop', version: '1.6.2'

    testImplementation checkstyleAddonsLibs.junit
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.27.0'

    spotbugsPlugins group: 'com.mebigfatguy.sb-contrib', name: 'sb-contrib', version: '7.4.7'
}


// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
//   Compilation
// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

java {
    sourceCompatibility = JavaVersion.VERSION_1_7
    disableAutoTargetJvm()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:unchecked'
    if (it.name != JavaPlugin.COMPILE_JAVA_TASK_NAME) {
        options.compilerArgs << '-Xlint:deprecation'
    }
}


// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
//   SpotBugs configuration
// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

spotbugs {
    toolVersion = checkstyleAddonsLibs.versions.spotbugs.get()
    effort = 'max'
    reportLevel = 'low'
    ignoreFailures = false
    includeFilter = file('config/spotbugs.xml')
    excludeFilter = file('config/spotbugs-excludes.xml')
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask.class) {
    it.setGroup(JavaBasePlugin.VERIFICATION_GROUP)
    reports {
        xml.enabled = false
        html {
            required = true
            stylesheet = 'fancy-hist.xsl'
        }
    }
}


// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
//   IntelliJ IDEA
// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

idea.module {
    downloadSources = true
    downloadJavadoc = true
    excludeDirs += file('.idea')
    excludeDirs += file('.jekyll-cache')
    excludeDirs += file('_posts')
    excludeDirs += file('_site')
    excludeDirs += file('_support')
}


// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
//   Checkstyle configuration for checking our own code
// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

checkstyle {
    configDirectory = file('config')
    configProperties 'workspace_loc': project.projectDir
    toolVersion = '7.1.2'    // depends on our checkstyle.xml; has nothing to do with the CS version used for building
    showViolations = true
    ignoreFailures = false
}


// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
//   JaCoCo
// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

def excludedClasses = ['CheckstyleApiFixer', 'MdlJsonConfigValidator']
tasks.named('test').configure {
    jacoco {
        excludes = excludedClasses
    }
}
tasks.named('jacocoTestReport', JacocoReport).configure { jacocoReportTask ->
    jacocoReportTask.dependsOn(tasks.named('test'))
    jacocoReportTask.reports {
        html.required = true
        xml.required = true
        csv.required = false
    }
    jacocoReportTask.sourceSets(sourceSets.main, sourceSets.sonarqube)
    jacocoReportTask.classDirectories.setFrom((FileCollection) fileTree(
        dir: sourceSets.main.output.classesDirs.first()).exclude(excludedClasses.collect{"**/$it*"}))
    jacocoReportTask.additionalClassDirs.setFrom(fileTree(dir: sourceSets.sonarqube.output.classesDirs.first()))
}


// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
//   Publishing of artifacts
// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

task checkProperties {
    setDescription('Ensure that all properties required for bintray upload are set')
    setGroup(PublishingPlugin.PUBLISH_TASK_GROUP)
    doLast {
        ['bintray.user', 'bintray.apikey', 'bintray.gpg.passphrase', 'sonatype.user', 'sonatype.password'].each {
            final String prp ->
                if (!project.hasProperty(prp)) {
                    throw new GradleException("Could not find property \'${prp}\'. " +
                        'Did you forget to specify it in ~/.gradle/gradle.properties?')
                }
        }
    }
}

bintray {
    if (project.hasProperty('bintray.user') && project.hasProperty('bintray.apikey')
        && project.hasProperty('bintray.gpg.passphrase') && project.hasProperty('sonatype.user')
        && project.hasProperty('sonatype.password'))
    {
        user = project.property('bintray.user')
        key = project.property('bintray.apikey')

        /* Set<String> pubNames = project.ext.depConfigs.publications.keySet()
         // TODO publish directly to maven central */
        Set<String> pubNames = [ 'checkstyle-addons',
                                 'checkstyle-addons-java7', 'checkstyle-addons-java8a', 'checkstyle-addons-java8b' ]
        publications = pubNames.toArray(new String[pubNames.size()])
        publish = true
        dryRun = true   // comment out for actual release deployment
        pkg {
            repo = 'checkstyle-addons'
            userOrg = 'checkstyle-addons'
            name = project.name
            desc = project.description
            websiteUrl = checkstyleAddons.website
            issueTrackerUrl = checkstyleAddons.issueTrackerUrl
            vcsUrl = "https://github.com/${checkstyleAddons.github}.git"
            licenses = ['GPL-3.0']
            publicDownloadNumbers = true
            version {
                name = project.version  // Bintray logical version name
                desc = project.description
                released = checkstyleAddons.buildTimestamp
                vcsTag = 'v' + project.version
                gpg {
                    sign = true
                    passphrase = project.property('bintray.gpg.passphrase')
                }
                mavenCentralSync {
                    sync = true
                    user = project.property('sonatype.user')
                    password = project.property('sonatype.password')
                }
            }
        }
    }
}
tasks['bintrayUpload'].dependsOn('checkProperties')
